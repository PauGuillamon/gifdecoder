plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.21'
    id "org.jlleitschuh.gradle.ktlint" version "9.4.1"
    id "com.github.ben-manes.versions" version "0.36.0"
}

group 'net.redwarp.gif'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.squareup.okio:okio:2.9.0"

    testImplementation "org.junit.jupiter:junit-jupiter:5.7.0"
    testImplementation "com.github.zh79325:open-gif:1.0.4"
}

sourceSets {
    main.java.srcDir("src/main/kotlin")
    test {
        java {
            srcDir 'src/test/kotlin'
        }
        resources {
            srcDirs += 'assets'
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ["-Xopt-in=kotlin.ExperimentalUnsignedTypes"]
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ["-Xopt-in=kotlin.ExperimentalUnsignedTypes"]
    }
}

test {
    useJUnitPlatform()
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    // Example 1: reject all non stable versions
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}